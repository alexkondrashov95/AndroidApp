
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.ГруппаСписокУпражнений.Видимость = Ложь;
	Элементы.ГруппаКалендарь.Видимость = Истина;
	Элементы.ГруппаФотоотчет.Видимость = Ложь;
	
	Кнопки = "Календарь";
	
	//Заполнение упражнений 
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ГруппыМышц.Ссылка КАК ГруппаМышц
	               |ИЗ
	               |	Справочник.ГруппыМышц КАК ГруппыМышц
				   |ГДЕ
				   |	НЕ ГруппыМышц.ПометкаУдаления";
	
	РЗ = Запрос.Выполнить().Выгрузить();
	
	СписокУпражнений.Очистить();
	Для Каждого ТекСтрока Из РЗ Цикл 		
		НовСтр = СписокУпражнений.Добавить();
		НовСтр.ГруппаМышц = ТекСтрока.ГруппаМышц;	
	КонецЦикла;
	
	Элементы.Календарь.НачалоПериодаОтображения = НачалоМесяца(ТекущаяДата());
	Элементы.Календарь.КонецПериодаОтображения = КонецМесяца(ТекущаяДата());
	
	ОбновитьСписокДатТренировок();
	ОбновитьСписокФотоотчет();
				
КонецПроцедуры

&НаКлиенте
Процедура КнопкиПриИзменении(Элемент)
	
	Элементы.ГруппаСписокУпражнений.Видимость = Ложь;
	Элементы.ГруппаКалендарь.Видимость = Ложь;
	Элементы.ГруппаФотоотчет.Видимость = Ложь;
	
	Если Кнопки = "Упражнения" Тогда
		Элементы.ГруппаСписокУпражнений.Видимость = Истина;		
	ИначеЕсли Кнопки = "Календарь" Тогда
		Элементы.ГруппаКалендарь.Видимость = Истина;		
	ИначеЕсли Кнопки = "Фотоотчет" Тогда
		Элементы.ГруппаФотоотчет.Видимость = Истина;		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КалендарьВыбор(Элемент, ВыбраннаяДата)
	
	ПараметрыВыбора = Новый Структура("Дата", ВыбраннаяДата);
    ОткрытьФорму("Документ.Тренировка.ФормаСписка", ПараметрыВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьТренировку(Команда)
	
	Если Элементы.Календарь.ВыделенныеДаты.Количество() <> 0 Тогда
		ТекДата = Элементы.Календарь.ВыделенныеДаты[0];
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("Дата", ТекДата);
	ОткрытьФорму("Документ.Тренировка.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СделатьФото(Команда)
	
#Если МобильноеПриложениеКлиент Тогда
	Если СредстваМультимедиа.ПоддерживаетсяФотоснимок() Тогда
		ДанныеМультимедиа = СредстваМультимедиа.СделатьФотоснимок();
		Данные = ДанныеМультимедиа.ПолучитьДвоичныеДанные();
		Комментарий = "Фото" + Формат(ТекущаяДата(),"ДФ=ddMMyyyy");
		ЗаписатьДанныеВДокумент(Данные, Комментарий);
	Иначе    
 		Сообщить(НСтр("ru = 'Съемка фото не поддерживается.'", "ru"));
	КонецЕсли; 
#КонецЕсли
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьДанныеВДокумент(Данные, Комментарий)
	
	НовДок = Документы.СделанноеФото.СоздатьДокумент();
	НовДок.Дата = ТекущаяДата();
	НовДок.Фото = Новый ХранилищеЗначения(Данные);
	НовДок.Комментарий = Комментарий;
	НовДок.Записать(РежимЗаписиДокумента.Проведение);
		
КонецПроцедуры

&НаКлиенте
Процедура КалендарьПриВыводеПериода(Элемент, ОформлениеПериода)
	
	Для Каждого ТекДата Из ОформлениеПериода.Даты Цикл
		Если  СписокДат.НайтиСтроки(Новый Структура("Дата",ТекДата.Дата)).Количество() > 0 Тогда
			ТекДата.ЦветФона = WebЦвета.Желтый;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия =  "ОбновлениеФормы" Тогда
		ОбновитьСписокДатТренировок();
		Элементы.Календарь.Обновить();		
	ИначеЕсли ИмяСобытия =  "ОбновитьФормуФото" Тогда
		ОбновитьСписокФотоотчет();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокФотоотчет()
	
	//Заполнение фотоальбома
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	СделанноеФото.Дата,
	               |	СделанноеФото.Комментарий
	               |ИЗ
	               |	Документ.СделанноеФото КАК СделанноеФото
				   |ГДЕ
				   |	НЕ СделанноеФото.ПометкаУдаления";	
	
	РЗ = Запрос.Выполнить().Выгрузить();
	
	СписокФотоотчет.Очистить();
	Для Каждого ТекСтрока Из РЗ Цикл
		НовСтр = СписокФотоотчет.Добавить();
		НовСтр.Дата = ТекСтрока.Дата;		
		НовСтр.Комментарий = ТекСтрока.Комментарий;		
	КонецЦикла;

КонецПроцедуры


&НаСервере
Процедура ОбновитьСписокДатТренировок()
	
	//Заполнение списка дат с тренировками
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	НАЧАЛОПЕРИОДА(Тренировка.Дата, ДЕНЬ) КАК Дата
	               |ИЗ
	               |	Документ.Тренировка КАК Тренировка
				   |ГДЕ
				   |	НЕ Тренировка.ПометкаУдаления";	
	
	РЗ = Запрос.Выполнить().Выгрузить();
	
	СписокДат.Очистить();
	Для Каждого ТекСтрока Из РЗ Цикл
		НовСтр = СписокДат.Добавить();
		НовСтр.Дата = ТекСтрока.Дата;		
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура СписокУпражненийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
		
	Если Элементы.СписокУпражнений.ТекущиеДанные = Неопределено Тогда
		
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("ГруппаМышц",Элементы.СписокУпражнений.ТекущиеДанные.ГруппаМышц);
	ОткрытьФорму("Справочник.Упражнения.ФормаСписка", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокФотоотчетВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элементы.СписокФотоотчет.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекДанные = Элементы.СписокФотоотчет.ТекущиеДанные;
	
	ПараметрыФормы = Новый Структура("Дата,Комментарий",ТекДанные.Дата,ТекДанные.Комментарий);
	ОткрытьФорму("Документ.СделанноеФото.Форма.ФормаФотоотчет", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры
